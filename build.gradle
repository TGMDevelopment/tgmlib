/*
 * Copyright (C) MatthewTGM
 * This file is part of TGMLib <https://github.com/TGMDevelopment/TGMLib>.
 *
 * TGMLib is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TGMLib is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TGMLib. If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            name = "Forge"
            url = "http://maven.minecraftforge.net"
        }
        maven {
            name = "JitPack"
            url = "https://jitpack.io"
        }
        maven {
            name = "Sponge"
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'com.github.asbyth:ForgeGradle:6f5327738d'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'com.github.LxGaming:MixinGradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

archivesBaseName = project.mod_name
version = project.mod_ver
group = project.mod_group

sourceCompatibility = targetCompatibility = 1.8

Dependency[] deps = [
        new Dependency("xyz.matthewtgm", "JsonTGM", "1.6", true, (String[])["gson"]),
        new Dependency("xyz.matthewtgm", "TGMConfig", "1.7.3", true),
        new Dependency("org.spongepowered", "mixin", "0.7.11-SNAPSHOT", true, (String[])["launchwrapper", "guava", "gson", "commons-io"], true),
        new Dependency("org.projectlombok", "lombok", "1.18.16", false, true)
]

sourceSets {
    main {
        output.resourcesDir = "build/classes/main"
    }
}

repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven/"
    }
}

minecraft {
    version = project.forge_ver
    runDir = "run"

    replace("@VER@", project.mod_ver)

    mappings = "stable_22"
}

dependencies {
    for (Dependency dep : deps) {
        compile(dep.getFullDep()) {
            if (dep.getExcluded().length > 0) {
                for (String excluded : dep.getExcluded()) {
                    exclude module: excluded
                }
            }
        }
        if (dep.isAnnotationProc()) annotationProcessor(dep.getFullDep())
    }
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependencies {
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))

        for (Dependency dep : deps)
            if (dep.isShade())
                include(dependency(dep.getFullDep()))
    }

    exclude('LICENSE.txt')
    exclude('dummyThing')
    exclude('module-info.class')

    classifier = ''
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            'TweakClass': 'xyz.matthewtgm.libs.mixins.TGMLibTweaker',
            'MixinConfigs': 'mixins.tgmlib.json',
            'TweakOrder': 0
    )

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

mixin {
    defaultObfuscationEnv = "searge"
    add sourceSets.main, "mixins.tgmlib.refmap.json"
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Custom dependency stuff. */
@SuppressWarnings("unused")
class Dependency {

    String group
    String name
    String version
    String[] excluded
    boolean shade
    boolean annotationProc
    final String fullDep

    Dependency(String group, String name, String version, boolean shade) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = []
        this.annotationProc = false

        this.fullDep = group + ":" + name + ":" + version
    }
    Dependency(String group, String name, String version, boolean shade, String[] excluded) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = excluded
        this.annotationProc = false

        this.fullDep = group + ":" + name + ":" + version
    }
    Dependency(String group, String name, String version, boolean shade, boolean annotationProc) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = []
        this.annotationProc = annotationProc

        this.fullDep = group + ":" + name + ":" + version
    }
    Dependency(String group, String name, String version, boolean shade, String[] excluded, boolean annotationProc) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = excluded
        this.annotationProc = annotationProc

        this.fullDep = group + ":" + name + ":" + version
    }

}