/*
 * Copyright (C) MatthewTGM
 * This file is part of TGMLib <https://github.com/TGMDevelopment/TGMLib>.
 *
 * TGMLib is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TGMLib is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TGMLib. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "net.minecraftforge.gradle.forge" version "6f5327738d"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.spongepowered.mixin" version "0.6-SNAPSHOT"
    id "java"
}

archivesBaseName = project.mod_name
version = project.mod_ver
group = project.mod_group

sourceCompatibility = targetCompatibility = 1.8

Dependency[] deps = [
        new Dependency("xyz.matthewtgm", "JsonTGM", "1.6", true, (String[])["gson"]),
        new Dependency("xyz.matthewtgm", "TGMConfig", "1.7.3", true),
        new Dependency("org.spongepowered", "mixin", "0.7.11-SNAPSHOT", true, (String[])["launchwrapper", "guava", "gson", "commons-io"], true),
        new Dependency("org.projectlombok", "lombok", "1.18.16", false, true)
]

minecraft {
    version = project.forge_ver
    runDir = "run"
    replace("@VER@", project.mod_ver)
    mappings = "stable_22"
}

/* Add available repositories to areas where dependencies can be downloaded. */
repositories {
    mavenCentral()
    maven {
        name = "SpongePowered"
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io/'
    }
}

/* Adds a configuration for shading libraries. */
configurations {
    shade
    compileOnly.extendsFrom(shade)
}

/* Define dependencies, also where dependencies are automatically handled. */
dependencies {
    for (Dependency dep : deps) {
        if (dep.isShade()) shade(dep.getFullDep()) {
            if (dep.getExcluded() != null && dep.getExcluded().length > 0)
                for (String excluded : dep.getExcluded())
                    exclude module: excluded
        }
        else implementation(dep.getFullDep()) {
            if (dep.getExcluded() != null && !Arrays.asList(dep.getExcluded()).isEmpty())
                for (String excluded : dep.getExcluded())
                    exclude module: excluded
        }

        if (dep.getAnnotationProc()) annotationProcessor(dep.getFullDep())
    }
}

/* Disable mixin refMap warnings. */
mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv = 'searge'
    add sourceSets.main, 'mixins.tgmlib.refmap.json'
}

/* Set the duplicatesStrategy and define configs.  */
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            'TweakClass': 'xyz.matthewtgm.libs.mixins.TGMLibTweaker',
            'MixinConfigs': 'mixins.tgmlib.json',
            'TweakOrder': 0
    )
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

/* Makes the shade configuration work and re-define the duplicatesStrategy. */
shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set('')
}

/* Force the Gradle wrapper version and distrbution type. */
wrapper {
    gradleVersion = "6.1"
    distributionType = Wrapper.DistributionType.ALL
}

/* Process the mcmod.info. */
processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Move the resources to the working directory for runs. */
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
reobfJar.dependsOn(shadowJar)

/* Custom dependency stuff. */
@SuppressWarnings("unused")
class Dependency {

    String group
    String name
    String version
    String[] excluded
    boolean shade
    boolean annotationProc
    final String fullDep

    Dependency(String group, String name, String version, boolean shade) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = []
        this.annotationProc = false

        this.fullDep = group + ":" + name + ":" + version
    }

    Dependency(String group, String name, String version, boolean shade, String[] excluded) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = excluded
        this.annotationProc = false

        this.fullDep = group + ":" + name + ":" + version
    }

    Dependency(String group, String name, String version, boolean shade, boolean annotationProc) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = []
        this.annotationProc = annotationProc

        this.fullDep = group + ":" + name + ":" + version
    }

    Dependency(String group, String name, String version, boolean shade, String[] excluded, boolean annotationProc) {
        this.group = group
        this.name = name
        this.version = version
        this.shade = shade
        this.excluded = excluded
        this.annotationProc = annotationProc

        this.fullDep = group + ":" + name + ":" + version
    }

}