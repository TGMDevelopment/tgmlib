/*
 * Copyright (C) MatthewTGM
 * This file is part of TGMLib <https://github.com/TGMDevelopment/TGMLib>.
 *
 * TGMLib is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TGMLib is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TGMLib. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "net.minecraftforge.gradle.forge" version "8708bf3e01"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "java"
}

archivesBaseName = mod_name
version = mod_ver
group = mod_group

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = forge_ver
    runDir = "run"
    replace('@NAME@', mod_name)
    replace('@ID@', mod_id)
    replace('@VER@', mod_ver)
    replace('@DEV@', mod_dev)
    mappings = "stable_22"
}

/* Add available repositories to areas where dependencies can be downloaded. */
repositories {
    mavenCentral()
    maven {
        name = "SpongePowered"
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io/'
    }
}

/* Adds a configuration for shading libraries. */
configurations {
    shade
    implementation.extendsFrom(shade)
}

/* Define dependencies. */
dependencies {
    /* TGMLib dependencies. */
    shade('xyz.matthewtgm:JsonTGM:2.8.0')
    shade('xyz.matthewtgm:TGMConfig:2.3.1')
    shade('org.java-websocket:Java-WebSocket:1.5.2')
    implementation('org.projectlombok:lombok:1.18.20')
    annotationProcessor('org.projectlombok:lombok:1.18.20')

    /* Dependencies for cutting other mods' file sizes. */
    shade('com.github.marcoacierno:pastebin-java-api:v0.2.1')
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    shade('com.squareup.okhttp3:okhttp:3.9.1')
    shade('org.kohsuke:github-api:1.132')
}

/* Set the duplicatesStrategy and define configs. */
jar {
    enabled = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            'FMLCorePlugin': 'xyz.matthewtgm.tgmlib.core.mod.TGMLibLoadingPlugin',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'ModSide': 'CLIENT'
    )
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

/* Makes the shade configuration work and re-define the duplicatesStrategy. */
shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    relocate('com.besaba.revonline', mod_shaded + 'besaba.revonline')
    relocate('okio', mod_shaded + 'okio')
    relocate('okhttp3', mod_shaded + 'okhttp3')
    relocate('org.spongepowered', mod_shaded + 'spongepowered')
    relocate('org.java_websocket', mod_shaded + 'websocket')

    archiveClassifier.set('')
}

/* Force the Gradle wrapper version and distribution type. */
wrapper {
    gradleVersion = "6.8.1"
    distributionType = Wrapper.DistributionType.ALL
}

/* Make shadow reobfuscate using SEARGE. */
reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

/* Process the mcmod.info. */
processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'name', mod_name
    inputs.property 'modid', mod_id
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand (
                'version':project.version,
                'mcversion':project.minecraft.version,
                'name':mod_name,
                'modid': mod_id
        )
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename('(.+_at.cfg)', 'META-INF/$1')
}

/* Move the resources to the working directory for runs. */
task moveResources {
    doLast {
        try {
            ant.move file: "${buildDir}/resources/main",
                    todir: "${buildDir}/classes/java"
        } catch(Exception e) {
            e.printStackTrace()
        }
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
jar.finalizedBy(shadowJar)