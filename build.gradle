/*
 * Copyright (C) MatthewTGM
 * This file is part of TGMLib <https://github.com/TGMDevelopment/TGMLib>.
 *
 * TGMLib is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TGMLib is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TGMLib. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "net.minecraftforge.gradle.forge" version "6f5327738d"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.spongepowered.mixin" version "d75e32e"
    id "java"
}

archivesBaseName = project.mod_name
version = project.mod_ver
group = project.mod_group
def modId = project.mod_id

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = project.forge_ver
    runDir = "run"
    replace("@VER@", project.mod_ver)
    mappings = "stable_22"
}

/* Add available repositories to areas where dependencies can be downloaded. */
repositories {
    mavenCentral()
    maven {
        name = "SpongePowered"
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io/'
    }
}

/* Adds a configuration for shading libraries. */
configurations {
    shade
    compileOnly.extendsFrom(shade)
}

/* Define dependencies, also where dependencies are automatically handled. */
dependencies {
    shade('xyz.matthewtgm:JsonTGM:1.6') {
        exclude module: 'gson'
    }
    shade('xyz.matthewtgm:TGMConfig:1.7.3')
    compileOnly('org.projectlombok:lombok:1.18.16')
    annotationProcessor('org.projectlombok:lombok:1.18.16')
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

/* Disable mixin refMap warnings. */
mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv = 'searge'
    add sourceSets.main, 'mixins.tgmlib.refmap.json'
}

/* Set the duplicatesStrategy and define configs.  */
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            'TweakClass': 'xyz.matthewtgm.libs.mixins.TGMLibTweaker',
            'MixinConfigs': 'mixins.tgmlib.json',
            'TweakOrder': 0
    )
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

/* Makes the shade configuration work and re-define the duplicatesStrategy. */
shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set('')
}

/* Force the Gradle wrapper version and distrbution type. */
wrapper {
    gradleVersion = "6.1"
    distributionType = Wrapper.DistributionType.ALL
}

/* Process the mcmod.info. */
processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/* Move the resources to the working directory for runs. */
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

task moveRefmap {
    doLast {
        if (file("${buildDir}/tmp/compileJava/mixins.${modId}.refmap.json").exists()) {
            ant.move file: "${buildDir}/tmp/compileJava/mixins.${modId}.refmap.json",
                    todir: "${buildDir}/classes/java"
        }
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
reobfJar.dependsOn(moveRefmap)
reobfJar.dependsOn(shadowJar)